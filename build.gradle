group 'cc.vileda'
version '0.0.2'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'kotlin-openapi3-dsl'
            description 'Build your OpenApi3 spec in kotlin!'
            url 'https://github.com/derveloper/kotlin-openapi3-dsl'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'cc.vileda'
            artifactId 'kotlin-openapi3-dsl'
            version "$version"

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/derveloper/kotlin-openapi3-dsl.git'
                url 'https://github.com/derveloper/kotlin-openapi3-dsl'

            }
            developers {
                developer {
                    name 'Tristan Leo'
                }
            }
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'cc.vileda'
            artifactId 'kotlin-openapi3-dsl'
            version "$version"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'kotlin-openapi3-dsl'
        userOrg = 'vileda'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/derveloper/kotlin-openapi3-dsl.git'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'
    compile 'com.fasterxml.jackson.module:jackson-module-jsonSchema:2.9.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.9.2'
    compile group: 'com.reprezen.kaizen', name: 'openapi-parser', version: '0.0.2.201710151503'
    compile 'io.vertx:vertx-web:3.5.0'
    compile 'io.vertx:vertx-web-api-contract:3.5.0'
    compile group: 'io.vertx', name: 'vertx-lang-kotlin', version: '3.5.0'
    compile group: 'io.vertx', name: 'vertx-rx-java2', version: '3.5.0'
    compile 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    testCompile 'io.kotlintest:kotlintest:2.0.7'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}